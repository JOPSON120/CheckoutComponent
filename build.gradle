plugins {
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.3'
    id 'java'
}

group = 'checkout.component'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation platform('org.junit:junit-bom:5.10.0')
    testImplementation 'org.junit.jupiter:junit-jupiter'

    // Spring Boot Starter for Web
    implementation 'org.springframework.boot:spring-boot-starter-web'
    // Spring Boot test starter (JUnit, AssertJ, Mockito, MockMvc, etc.)
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    // RestAssured for acceptance tests
    testImplementation 'io.rest-assured:rest-assured'

    // Lombok
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30'
    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'
}

test {
    useJUnitPlatform()
}

tasks.register('cleanup', Delete) {
    group = 'clean'
    delete "$projectDir/build/libs/lib"
}

tasks.register('extractDependencies', Copy) {
    dependsOn cleanup

    description = 'Extracts all runtime dependencies to a directory'
    group = 'build'
    into "$projectDir/build/libs/lib"

    from {
        configurations.runtimeClasspath
    }
}

jar {
    dependsOn extractDependencies

    archiveBaseName.set('CheckoutComponent.jar')
    archiveVersion.set("1.0.0")
    archiveClassifier.set('release')
    destinationDirectory.set(file("$projectDir/build/libs"))
    from sourceSets.main.output

    manifest {
        attributes(
                'Main-Class': 'checkout.component.app.CheckoutComponent',
                'Class-Path': configurations.runtimeClasspath.files.collect { 'lib/' + it.name }.join(' ')
        )
    }
}